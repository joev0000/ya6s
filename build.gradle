plugins {
  id 'application'
  id 'com.github.spotbugs' version '6.1.7'
  id 'org.beryx.jlink' version '3.0.1'
}

version = '1.0.2'

application {
  mainModule = 'joev.ya6s'
  mainClass = 'joev.ya6s.Main'
  applicationDefaultJvmArgs = ['-Djava.lang.Integer.IntegerCache.high=65535']
}

java {
  modularity.inferModulePath.set(true)
}

repositories {
  mavenCentral()
}

dependencies {
  // https://mvnrepository.com/artifact/net.java.dev.jna/jna
  implementation group: 'net.java.dev.jna', name: 'jna', version: '5.14.0'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
  testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.10.2'
  testRuntimeOnly    'org.junit.platform:junit-platform-launcher'
}

compileJava {
  options.compilerArgs += [
    '-encoding', 'UTF-8',
    '--release', '17',
    '-Xlint:all',
    '-Xlint:-requires-automatic',
    '-Xlint:-requires-transitive-automatic',
    '-Werror']
}

compileTestJava {
  options.compilerArgs += [
    '-Xlint:all',
    '-Xlint:-requires-automatic',
    '-Xlint:-requires-transitive-automatic',
    '-Werror']
}

run {
  standardInput = System.in
}

test {
  useJUnitPlatform()
}

spotbugs {
  excludeFilter = file("spotbugs-exclude.xml")
}

jlink {
  options = ['--no-header-files', '--no-man-pages', '--strip-debug']
  mergedModuleName = 'joev.ya6s.merged'
  imageName = name + '-' + version
  mergedModule {
    excludeRequires 'java.desktop'
  }
}
