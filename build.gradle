plugins {
  id 'application'
  id 'com.intershop.gradle.javacc' version '4.0.0'
}

application {
  mainModule = 'joev.ya6s'
  mainClass = 'joev.ya6s.Main'
}

java {
  modularity.inferModulePath.set(true)
}

repositories {
  mavenCentral()
}

dependencies {
  // https://mvnrepository.com/artifact/net.java.dev.jna/jna
  implementation group: 'net.java.dev.jna', name: 'jna', version: '5.7.0'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
  testRuntimeOnly    'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

compileJava {
  options.compilerArgs += [
    '-g',
    '-encoding', 'UTF-8',
    '-Xlint:all',
    '-Xlint:-requires-automatic',
    '-Xlint:-requires-transitive-automatic',
    '-Werror']
}

compileTestJava {
  options.compilerArgs += [
    '-Xlint:all',
    '-Xlint:-requires-automatic',
    '-Xlint:-requires-transitive-automatic',
    '-Werror']
}

javacc {
  javaCCVersion = '7.0.9'
  configs {
    monitor {
      inputFile = file('src/main/java/joev/ya6s/monitor/parser.jj')
      packageName = 'joev.ya6s.monitor'
    }
  }
}

run {
  standardInput = System.in
}

test {
  useJUnitPlatform()
}
